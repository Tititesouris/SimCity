to initResourceConsumer
  set electricityStored 0
  set waterStored 0
end

to updateResourceConsumer
  if time mod hourLength = 0 [
    let :target self
    let :warnings warnings with [target = :target]
    let :electricityWarnings :warnings with [shape = warningElectricity]
    let :waterWarnings :warnings with [shape = warningWater]

    let :x xcor - 0.5 + (0.5 * (count :warnings))
    let :y ycor + 0.5
    let :importanceStep 6 * hourLength

    ifelse electricityStored > 0 [
      set electricityStored electricityStored - electricityUsage
      ask :electricityWarnings [die]
    ][
      if (electricityUsage > 0) and (not any? :electricityWarnings) [
        hatch-warnings 1 [initWarning :x :y warningElectricity :target :importanceStep]
        set :x :x + 0.5
      ]
    ]

    ifelse waterStored > 0 [
      set waterStored waterStored - waterUsage
      ask :waterWarnings [die]
    ][
      if (waterUsage > 0) and (not any? :waterWarnings) [
        hatch-warnings 1 [initWarning :x :y warningWater :target :importanceStep]
      ]
    ]
  ]

  let electricityStorage electricityUsage * 2
  if electricityStored < electricityStorage [
    let :electricitiesAround electricities in-radius 1.5
    ask :electricitiesAround [
      let :needed [electricityStorage - electricityStored] of myself
      ifelse amount <= :needed [
        ask myself [set electricityStored electricityStored + [amount] of myself]
        ask electricities with [group = [group] of myself] [die]
      ][
        ask myself [set electricityStored electricityStorage]
        ask electricities with [group = [group] of myself] [set amount amount - :needed]
      ]
    ]
  ]

  let waterStorage waterUsage * 2
  if waterStored < waterUsage [
    let :watersAround waters in-radius 1.5
    ask :watersAround [
      let :needed [waterStorage - waterStored] of myself
      ifelse amount <= :needed [
        ask myself [set waterStored waterStored + [amount] of myself]
        ask waters with [group = [group] of myself] [die]
      ][
        ask myself [set waterStored waterStorage]
        ask waters with [group = [group] of myself] [set amount amount - :needed]
      ]
    ]
  ]
end
