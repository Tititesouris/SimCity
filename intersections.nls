breed [intersections intersection]
intersections-own [
  directions
]

to loadIntersections
  file-open "intersections.txt"
  while [not file-at-end?] [
    let :x file-read
    let :y file-read
    let :north file-read
    let :east file-read
    let :south file-read
    let :west file-read
    create-intersections 1 [initIntersection :x :y :north :east :south :west]
  ]
  file-close
end

to saveIntersections
  carefully [file-delete "intersections.txt"][]
  file-open "intersections.txt"
  ask intersections [
    let :north (member? (patch-at 0 1) directions)
    let :east (member? (patch-at 1 0) directions)
    let :south (member? (patch-at 0 -1) directions)
    let :west (member? (patch-at -1 0) directions)
    file-print (word pxcor " " pycor " " :north " " :east " " :south " " :west)
  ]
  file-close
end

to initIntersection [:x :y :north :east :south :west]
  setxy :x :y
  set heading 0
  set directions no-patches
  let :nbDirections 0
  let :direction 0
  if :north [
    set directions (patch-set directions (patch-at 0 1))
    set :nbDirections :nbDirections + 1
    set :direction 0
  ]
  if :east [
    set directions (patch-set directions (patch-at 1 0))
    set :nbDirections :nbDirections + 1
    set :direction 90
  ]
  if :south [
    set directions (patch-set directions (patch-at 0 -1))
    set :nbDirections :nbDirections + 1
    if not :west [
      set :direction 180
    ]
  ]
  if :west [
    set directions (patch-set directions (patch-at -1 0))
    set :nbDirections :nbDirections + 1
    if not :north [
      set :direction 270
    ]
  ]
  set shape word "intersection " :nbDirections
  set color black
  set heading :direction
end